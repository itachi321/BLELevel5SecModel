theory Note_for_Level5Sec_changed
begin

heuristic: S

// builtins: diffie-hellman, symmetric-encryption
// functions: 
builtins: symmetric-encryption, diffie-hellman, natural-numbers
functions: KDF/2 
// equations: 

// Oracles
rule bindOracle:
  [
    In(<'Bind',$Host,$Ctrller>)
  ]
  --[
    Neq($Host,$Ctrller),
    OnlyOnceV(<$Host,$Ctrller>)
  ]->
  [
    !HCBind($Ctrller,$Host)
  ]

rule pairingOracle:
  [
    In(<'Pairing',$HI,$HR>),
    !HCBind($CI,$HI),
    !HCBind($CR,$HR),
    Fr(~LTK)
  ]
  --[
    Partner($HI,$HR),
    Partner($HR,$HI)
  ]->
  [ 
    !HostStateP('Paired',<$HI,'Initiator',$CI,$HR,~LTK>),
    !HostStateP('Paired',<$HR,'Responder',$CR,$HI,~LTK>)
  ]

rule connectLLOracle:
  [
    In(<'Connect',$HI,$HR>),
    !HostStateP('Paired',<$HI,'Initiator',$CI,$HR,~LTK>),
    !HostStateP('Paired',<$HR,'Responder',$CR,$HI,~LTK>),
    !HCBind($CI,$HI),
    !HCBind($CR,$HR),
    Fr(~HCIHandleI),
    Fr(~HCIHandleR),
    Fr(~AccessAdd)
  ]
  --[]->
  [
    HostState('LLConnected',<$HI,'Initiator',$CI,$HR,~LTK,~HCIHandleI>),
    HostState('LLConnected',<$HR,'Responder',$CR,$HI,~LTK,~HCIHandleR>),
    !ControllerStateP('LLConnected',<$CI,$HI,~HCIHandleI,~AccessAdd>),
    !ControllerStateP('LLConnected',<$CR,$HR,~HCIHandleR,~AccessAdd>)
  ]

rule oracle:
  [
    !HostStateP('Paired',<$Host,Role,$C,$PeerHost,~LTK>),
    In(<'ReqLTK',$Host>)
  ]
  --[
    Reveal($Host)
  ]->
  [
    Out(~LTK)
  ]

// ! HCI Channel
rule hciChannelsend [color=#D3D3D3]:
  [
    HCIOut(src,dst,msg)
  ]
  --[]->
  [
    !HCIState(src,dst,msg)
  ]

rule hciChannelrev [color=#D3D3D3]:
  [
    !HCIState(src,dst,msg)
  ]
  --[]->
  [
    HCIIn(src,dst,msg)
  ]


// two rules are used for transmitting public key before encoding
rule hciInTranOut:
  [
    !ControllerStateP('LLConnected',<$C,$Host,~HCIHandle,~AccessAdd>),
    HCIIn($Host,$C,<~HCIHandle,<'ACL',DHEpk>>)
  ]
  --[]->
  [
    Out(<~AccessAdd,DHEpk>)
  ]

rule inTranHCIOut:
  [
    !ControllerStateP('LLConnected',<$C,$Host,~HCIHandle,~AccessAdd>),
    In(<~AccessAdd,DHEpk>)
  ]
  --[]->
  [
    HCIOut($C,$Host,<~HCIHandle,<'ACL',DHEpk>>)
  ]



//! Initiator Host
/*
PRE: HostState('LLConnected',<$HI,'Initiator',$CI,$HR,~LTK,~HCIHandleI>),
ACT: Fr(~DHEskI)
CON: HostState('SentDHEpk',<$HI,'Initiator',$CI,$HR,~LTK,~HCIHandleI,~DHEskI>),
OUT-HCI: HCIOut($HI,$CI,<~HCIHandleI,<'ACL',DHEpkI>>)
*/
rule ihSendDHEpk [color=#DEBF80]:
  let
    DHEpkI = 'g' ^ ~DHEskI
  in
  [
    HostState('LLConnected',<$HI,'Initiator',$CI,$HR,~LTK,~HCIHandleI>),
    Fr(~DHEskI)
  ]
  --[]->
  [
    HostState('SentDHEpk',<$HI,'Initiator',$CI,$HR,~LTK,~HCIHandleI,~DHEskI>),
    HCIOut($HI,$CI,<~HCIHandleI,<'ACL',DHEpkI>>)
  ]

/*
PRE: HostState('SentDHEpk',<$HI,'Initiator',$CI,$HR,~LTK,~HCIHandleI,~DHEskI>),
IN-HCI: HCIIn($CI,$HI,<~HCIHandleI,DHEpkR)
CON: HostState('SentLTK',<$HI,'Initiator',$CI,$HR,LMKE,~HCIHandleI>),
OUT-HCI: HCIOut($HI,$CI,<~HCIHandleI,LMKE>)
*/
rule ihSendLTK [color=#DEBF80]:
  let
    LMKE = KDF(~LTK,DHEpkR ^ ~DHEskI)
  in
  [
    HostState('SentDHEpk',<$HI,'Initiator',$CI,$HR,~LTK,~HCIHandleI,~DHEskI>),
    HCIIn($CI,$HI,<~HCIHandleI,DHEpkR>)
  ]
  --[]->
  [
    HCIOut($HI,$CI,<~HCIHandleI,LMKE>),
    HostState('SentLTK',<$HI,'Initiator',$CI,$HR,LMKE,~HCIHandleI>)
  ]

/*
PRE: HostState('SentLTK',<$HI,'Initiator',$CI,$HR,LTK,~HCIHandleI>),
IN-HCI: HCIIn($CI,$HI,<~HCIHandleI,<'HCI_Command_State','HCI_LE_Enable_Encryption'>>)
        HCIIn($CI,$HI,<~HCIHandleI,<'HCI_Encrytion_Change'>>)
CON: HostState('LLEncrypted',<$HI,'Initiator',$CI,$HR,LTK,~HCIHandleI>),
*/
rule ihRevEncChange [color=#DEBF80]:
  [
    HostState('SentLTK',<$HI,'Initiator',$CI,$HR,LTK,~HCIHandleI>),
    HCIIn($CI,$HI,<~HCIHandleI,<'HCI_Command_State','HCI_LE_Enable_Encryption'>>),
    HCIIn($CI,$HI,<~HCIHandleI,<'HCI_Encrytion_Change'>>)
  ]
  --[]->
  [
    HostState('LLEncrypted',<$HI,'Initiator',$CI,$HR,LTK,~HCIHandleI>)
  ]


//! Initiator Controller
/*
PRE: !ControllerStateP('LLConnected',<$CI,$HI,~HCIHandleI,~AccessAdd>),
ACT: Fr(~SKDI),Fr(~IVI)
IN-HCI: HCIIn($HI,$CI,<~HCIHandleI,LTK>),
CON: ControllerState('LLSentSKD',<$CI,$HI,~HCIHandleI,~AccessAdd,~SKDI,~IVI,LTK>),
OUT-BT: Out(<~AccessAdd,~SKDI,~IVI>),
OUT-HCI: HCIOut($CI,$HI,<~HCIHandleI,<'HCI_Command_State','HCI_LE_Enable_Encryption'>>)
*/
rule icRevLTK [color=#DCCD5B]:
  [
    !ControllerStateP('LLConnected',<$CI,$HI,~HCIHandleI,~AccessAdd>),
    HCIIn($HI,$CI,<~HCIHandleI,LTK>),
    Fr(~SKDI),
    Fr(~IVI)
  ]
  --[
    OnlyOnceV(<'RevLTK',~HCIHandleI>)
  ]->
  [
    ControllerState('LLSentSKD',<$CI,$HI,~HCIHandleI,~AccessAdd,~SKDI,~IVI,LTK>),
    Out(<~AccessAdd,~SKDI,~IVI>),
    HCIOut($CI,$HI,<~HCIHandleI,<'HCI_Command_State','HCI_LE_Enable_Encryption'>>)
  ]

/*
PRE: ControllerState('LLSentSKD',<$CI,$HI,~HCIHandleI,~AccessAdd,~SKDI,~IVI,LTK>),
IN-BT: In(<~AccessAdd,~SKDR,~IVR>),
        In(<~AccessAdd,'LL_START_ENC_REQ'>),
CON: ControllerState('LLSenEnctRSP',<$CI,$HI,~HCIHandleI,~AccessAdd,SK,IV,sendCounterNew,revCounter>),
OUT-BT: Out(<~AccessAdd,'LL_START_ENC_RSP'>)
 */
rule icRevSKD [color=#DCCD5B]:
  let
    directionFlagSend = '1'
    directionFlagRev = '0'
    sendCounter = %1
    revCounter = %1
    sendCounterNew = sendCounter %+ %1
    SK = KDF(LTK, <~SKDI,~SKDR>)
    IV = <~IVI,~IVR>
    EncRsp = senc(<<IV,sendCounter,directionFlagSend>,'LL_START_ENC_RSP'>,SK)
  in
  [
    ControllerState('LLSentSKD',<$CI,$HI,~HCIHandleI,~AccessAdd,~SKDI,~IVI,LTK>),
    In(<~AccessAdd,~SKDR,~IVR>),
    In(<~AccessAdd,'LL_START_ENC_REQ'>)
  ]
  --[]->
  [
    ControllerState('LLSentEncRSP',<$CI,$HI,~HCIHandleI,~AccessAdd,SK,IV,sendCounterNew,revCounter>),
    Out(<~AccessAdd,EncRsp>)
  ]

/*
PRE: ControllerState('LLSentEncRSP',<$CI,$HI,~HCIHandleI,~AccessAdd,SK,IV,sendCounterNew,revCounter>),
IN-BT: In(<~AccessAdd,EncPeerRsp>)，
CON: ControllerState('LLRevEncRSP',<$CI,$HI,~HCIHandleI,~AccessAdd,SK,IV,sendCounterNew,revCounterNew>),
OUT-HCI：HCIOut($CI,$HI,<~HCIHandleI,<'HCI_Encrytion_Change'>>)
*/
rule icRevEncRSP [color=#DCCD5B]:
  let
    directionFlagSend = '1'
    directionFlagRev = '0'
    revCounterNew = %revCounter %+ %1
    EncPeerRsp = senc(<<IV,%revCounter,directionFlagRev>,'LL_START_ENC_RSP'>,SK)
  in
  [
    ControllerState('LLSentEncRSP',<$CI,$HI,~HCIHandleI,~AccessAdd,SK,IV,%sendCounter,%revCounter>),
    In(<~AccessAdd,EncPeerRsp>)
  ]
  --[
    Secret(SK),
    Honest($HI),
  ]->
  [
    ControllerState('LLRevEncRSP',<$CI,$HI,~HCIHandleI,~AccessAdd,SK,IV,%sendCounter,revCounterNew>),
    HCIOut($CI,$HI,<~HCIHandleI,<'HCI_Encrytion_Change'>>)
  ]


//! Responder Controller
/*
PRE: !ControllerStateP('LLConnected',<$CR,$HR,~HCIHandleR,~AccessAdd>),
IN-BT: In(<~AccessAdd,~SKDI,~IVI>),
ACT: Fr(~SKDR),Fr(~IVR)
CON: ControllerState('LLRevSKDI',<$CR,$HR,~HCIHandleR,~AccessAdd,~SKDI,~SKDR,~IVI,~IVR>),
OUT-BT: Out(<~AccessAdd,~SKDR,~IVR>),
OUT-HCI: HCIOut($CR,$HR,<~HCIHandleR,<'HCI_LE_Long_Term_Key_Request'>>)
*/
rule rcRevSKDI [color=#73AD96]:
  [
    !ControllerStateP('LLConnected',<$CR,$HR,~HCIHandleR,~AccessAdd>),
    In(<~AccessAdd,~SKDI,~IVI>),
    Fr(~SKDR),
    Fr(~IVR)
  ]
  --[]->
  [
    ControllerState('LLRevSKDI',<$CR,$HR,~HCIHandleR,~AccessAdd,~SKDI,~SKDR,~IVI,~IVR>),
    Out(<~AccessAdd,~SKDR,~IVR>),
    HCIOut($CR,$HR,<~HCIHandleR,<'HCI_LE_Long_Term_Key_Request'>>)
  ]

/*
PRE: ControllerState('LLRevSKDI',<$CR,$HR,~HCIHandleR,~AccessAdd,~SKDI,~SKDR,~IVI,~IVR>),
IN-HCI: HCIIn($HR,$CR,<~HCIHandleR,LTK>)
CON: ControllerState('LLRevLTK',<$CR,$HR,~HCIHandleR,~AccessAdd,SK,IV>),
OUT-BT: Out(<~AccessAdd,'LL_START_ENC_REQ'>),
OUT-HCI: HCIOut($CR,$HR,<~HCIHandleR,<'HCI_Command_Complete'>>)
*/
rule rcRevLTK [color=#73AD96]:
  let
    SK = KDF(LTK, <~SKDI,~SKDR>)
    IV = <~IVI,~IVR>
  in
  [
    ControllerState('LLRevSKDI',<$CR,$HR,~HCIHandleR,~AccessAdd,~SKDI,~SKDR,~IVI,~IVR>),
    HCIIn($HR,$CR,<~HCIHandleR,LTK>)
  ]
  --[
    Secret(SK),
    Honest($HR)
  ]->
  [
    ControllerState('LLRevLTK',<$CR,$HR,~HCIHandleR,~AccessAdd,SK,IV>),
    Out(<~AccessAdd,'LL_START_ENC_REQ'>),
    HCIOut($CR,$HR,<~HCIHandleR,<'HCI_Command_Complete'>>)
  ]

/*
PRE: ControllerState('LLRevLTK',<$CR,$HR,~HCIHandleR,~AccessAdd,SK,IV>),
IN-BT: In(<~AccessAdd,'LL_START_ENC_RSP'>),
CON: ControllerState('LLRevEncRSP',<$CR,$HR,~HCIHandleR,~AccessAdd,SK,,sendCounterNew,revCounterNew>),
OUT-BT: Out(<~AccessAdd,EncRsp>),
OUT-HCI: HCIOut($CR,$HR,<~HCIHandleR,<'HCI_Encrytion_Change'>>)
*/
rule rcRevEncRSP [color=#73AD96]:
  let
    directionFlagSend = '0'
    directionFlagRev = '1'
    sendCounter = %1
    sendCounterNew = sendCounter %+ %1
    revCounter = %1
    revCounterNew = revCounter %+ %1
    EncPeerRsp = senc(<<IV,revCounter,directionFlagRev>,'LL_START_ENC_RSP'>,SK)
    EncRsp = senc(<<IV,sendCounter,directionFlagSend>,'LL_START_ENC_RSP'>,SK)
  in
  [
    ControllerState('LLRevLTK',<$CR,$HR,~HCIHandleR,~AccessAdd,SK,IV>),
    In(<~AccessAdd,EncPeerRsp>)
  ]
  --[Finished()]->
  [
    ControllerState('LLRevEncRSP',<$CR,$HR,~HCIHandleR,~AccessAdd,SK,sendCounterNew,revCounterNew>),
    Out(<~AccessAdd,EncRsp>),
    HCIOut($CR,$HR,<~HCIHandleR,<'HCI_Encrytion_Change'>>)
  ]


//! Responder Host
/*
PRE: HostState('LLConnected',<$HR,'Responder',$CR,$HI,~LTK,~HCIHandleR>),
IN-HCI: HCIIn($CR,$HR,<~HCIHandleR,<'ACL',DHEpkI>>),
ACT: Fr(~DHEskR)
CON: HostState('LLSentDHEpk',<$HR,'Responder',$CR,$HI,~LTK,~HCIHandleR,~DHEskR,DHEpkI>),
OUT-HCI: HCIOut($HR,$CR,<~HCIHandleR,<'ACL',DHEpkR>>)
*/
rule rhSendDHEpk [color=#5F9069]:
  let
    DHEpkR = 'g' ^ ~DHEskR
  in
  [
    HostState('LLConnected',<$HR,'Responder',$CR,$HI,~LTK,~HCIHandleR>),
    HCIIn($CR,$HR,<~HCIHandleR,<'ACL',DHEpkI>>),
    Fr(~DHEskR)
  ]
  --[]->
  [
    HostState('LLSentDHEpk',<$HR,'Responder',$CR,$HI,~LTK,~HCIHandleR,~DHEskR,DHEpkI>),
    HCIOut($HR,$CR,<~HCIHandleR,<'ACL',DHEpkR>>)
  ]

/*
PRE: HostState('LLSentDHEpk',<$HR,'Responder',$CR,$HI,~LTK,~HCIHandleR,~DHEskR,DHEpkI>),
IN-HCI: HCIIn($CR,$HR,<~HCIHandleR,<'HCI_LE_Long_Term_Key_Request'>>)
CON: HostState('LLSentLTK',<$HR,'Responder',$CR,$HI,LMKE,~HCIHandleR>),
OUT-HCI: HCIOut($HR,$CR,<~HCIHandleR,LMKE>)
*/
rule rhRevLTKRequest [color=#5F9069]:
  let
    LMKE = KDF(~LTK,DHEpkI ^ ~DHEskR)
  in
  [
    HostState('LLSentDHEpk',<$HR,'Responder',$CR,$HI,~LTK,~HCIHandleR,~DHEskR,DHEpkI>),
    HCIIn($CR,$HR,<~HCIHandleR,<'HCI_LE_Long_Term_Key_Request'>>)
  ]
  --[]->
  [
    HostState('LLSentLTK',<$HR,'Responder',$CR,$HI,LMKE,~HCIHandleR>),
    HCIOut($HR,$CR,<~HCIHandleR,LMKE>)
  ]

/* 
PRE: HostState('LLSentLTK',<$HR,'Responder',$CR,$HI,LTK,~HCIHandleR>),
IN-HCI: HCIIn($CR,$HR,<~HCIHandleR,<'HCI_Command_Complete'>>),
        HCIIn($CR,$HR,<~HCIHandleR,<'HCI_Encrytion_Change'>>),
CON: HostState('LLEncrypted',<$HR,'Responder',$CR,$HI,LTK,~HCIHandleR>)
*/
rule rhRevEncChange [color=#5F9069]:
  [
    HostState('LLSentLTK',<$HR,'Responder',$CR,$HI,LTK,~HCIHandleR>),
    HCIIn($CR,$HR,<~HCIHandleR,<'HCI_Command_Complete'>>),
    HCIIn($CR,$HR,<~HCIHandleR,<'HCI_Encrytion_Change'>>)
  ]
  --[]->
  [
    HostState('LLEncrypted',<$HR,'Responder',$CR,$HI,LTK,~HCIHandleR>)
  ]

restriction Inequality:
  "
  All x #i. Neq(x,x) @i ==> F
  "
/* 
restriction Equality:
  "
  All x y #i. Eq(x,y) @i ==> x = y
  "
*/

restriction OnlyOnceV:
  "
  All x #i #j. OnlyOnceV(x)@i & OnlyOnceV(x)@j ==> #i = #j
  "

lemma Test:
  exists-trace
  "
  Ex #i. Finished()@i
  "

lemma secrecy_PFS [use_induction]:
  "All x #i.
    Secret(x)@i ==>
    not (Ex #j. K(x)@j) 
      | (Ex B #j. Reveal(B)@j & Honest(B)@i & j < i)
      | (Ex B BP #j #k. Reveal(BP)@j & Honest(B)@i & Partner(B,BP)@k & j < i)
  "

end